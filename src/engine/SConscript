import os
import sys
import glob

full_import_list ="globalenv localenv IsLinux IsWindows IsDarwin compiler librarydir binarydir"

Import(full_import_list)

files = [
  "engine.cpp",
  "functionload.cpp",
  "loadplugins.cpp",
  "typed_array.cpp",
  "variableregister.cpp",
  "version.cpp",
  "options.c",
]

environment = localenv

libyyymonitor = SConscript(dirs = ["platform/libyyymonitor"], exports = ["environment"])

enginelibs = [
  "t5",
  "graphiccommon",
  "graphicalg",
  "configmanager",
  "v8",
  libyyymonitor,
]

platformfiles = [
    "platform/platform.cpp",
    "platform/time.cpp",
    "platform/background.cpp"

]

if IsDarwin():
  platformfiles += ["platform/osx_time.m", "platform/osx_messagebox.m"]

enginelibs += [globalenv.StaticLibrary("turbo_patform", platformfiles)]

if not IsWindows():
  enginelibs+= ["dl"]
  enginelibs+= ["z"]

if IsDarwin():
  enginelibs += ["objc"]
  localenv.Append(FRAMEWORKS=['Cocoa', 'CoreFoundation'])
  globalenv.Append(FRAMEWORKS=['CoreFoundation'])

localenv.Append(LIBS = enginelibs)

turbosphere = globalenv.Program("turbosphere", source = files, LIBS = enginelibs)

globalenv.Install(binarydir, [turbosphere])

Return("turbosphere")

