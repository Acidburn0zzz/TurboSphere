Import('currentenv librarydir corelibtargets binarydir LibraryInstall IsDarwin IsLinux IsWindows IsSolaris jslib')

import os
import sys

gfx_backend = "GL"
gfx_version = 4

Sapphire_files = ["api.cpp", "GLStart.cpp",
    "SapphireInit.cpp", "SapphirePlugin.cpp", "SaveImage.cpp", 
    "script.cpp", "Surface/Primitives.cpp"]

if gfx_backend=="GL":
    Sapphire_files+=["Image/OpenGLImage.cpp"]
else:
    Sapphire_files+=["Image/SoftwareImage.cpp"] 

UseCpp11ThreadsAndAtomics = ARGUMENTS.get('cpp11_thread', 0)
UseSDL2ThreadsAndAtomics = ARGUMENTS.get('sdl2_thread', 0)
Uselibpng = ARGUMENTS.get('use_png', 1)
Uselibjpg = ARGUMENTS.get('use_jpg', 1)
Uselibjpeg = ARGUMENTS.get('use_jpeg', 1)
Uselibtiff = ARGUMENTS.get('use_tiff', 0)

Uselibtiff = 0


currentenv.Append(CPPDEFINES = ["USE_TGA", "USE_XPM"])
if(gfx_backend=="GL"):
    currentenv.Append(CPPDEFINES = ["OPENGL_"+str(gfx_version)])

if int(Uselibpng):
  currentenv.Append(CPPDEFINES = ["USE_PNG"])
if int(Uselibjpg) or int(Uselibtiff):
  currentenv.Append(CPPDEFINES = ["USE_TIFF"])
if int(Uselibjpg) or int(Uselibjpeg):
  currentenv.Append(CPPDEFINES = ["USE_JPEG"])
  if IsDarwin():
    currentenv.Append(LIBPATH = ["/usr/local/opt/jpeg-turbo/lib"])

elibs = []

eframeworks = []

if IsLinux():
  currentenv.Append(CPPDEFINES = ["USE_PTHREAD"])

Sapphire_files += [os.path.join("Thread", "Mutex_sdl2.c")]

if int(UseCpp11ThreadsAndAtomics):
  Sapphire_files += [os.path.join("Thread", "Thread_cpp11.cpp"), os.path.join("Thread", "Atomic_cpp11.cpp")]
elif int(UseSDL2ThreadsAndAtomics):
  Sapphire_files += [os.path.join("Thread", "Thread_sdl2.c"), os.path.join("Thread", "Atomic_sdl2.c")]
elif IsDarwin():
  Sapphire_files += [os.path.join("Thread", "Thread_nsthread.m"), os.path.join("Thread", "Atomic_darwin.c"), "RetinaCheck.m"]
  elibs = ["objc"]
  eframeworks = ["Foundation"]
if 'USE_INTEL_TBB' in currentenv['CPPDEFINES']:
  elibs+= ['tbb']

if int(Uselibjpg) or int(Uselibjpeg):
    elibs+=['jpeg']

environment = currentenv.Clone()
libyyymonitor = SConscript(dirs = ["libyyymonitor"], exports = ["environment"])
environment = currentenv.Clone()
libgalileo    = SConscript(dirs = ["Galileo"], exports = ["environment", "IsDarwin", "IsLinux", "IsWindows", "IsSolaris", "gfx_backend", "gfx_version"])

if gfx_backend=="GL":
    eframeworks += ['OpenGL']

Sapphire     = currentenv.SharedLibrary("Sapphire", Sapphire_files,
  LIBS = [libyyymonitor, libgalileo, 't5', 'graphiccommon', 'graphicalg', 'configmanager', 'SDL2', 'SDL2_image', jslib] + elibs,
  FRAMEWORKS = eframeworks)

Depends(Sapphire, corelibtargets)
LibraryInstall(os.path.join(binarydir, 'plugin'), Sapphire, currentenv)
