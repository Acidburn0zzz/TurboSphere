import os
import sys

full_import_list ="globalenv IsLinux IsWindows IsDarwin compiler binarydir localenv librarydir "

Import(full_import_list)

# Set up V8 includes.

globalenv.Append(CPPPATH = os.path.join(os.getcwd(), "thirdparty/v8/include"))

# Locate the TS Plugin SDK
globalenv.Append(CPPPATH = os.path.join(os.getcwd(), "pluginsdk"))

# Locate the TS Portable Runtime tools
globalenv.Append(CPPPATH = os.path.join(os.getcwd(), "TSPR"))

# Dependencies of TurboSphere, called in the precise order they _should_ be compiled in:
# T5, configmanager, graphiccommon, graphicalg,

# Explain the dependency chain to Scons, and pray it works out the way it should.

corelibs = [
  "graphiccommon",
  "graphicalg",
  "configmanager",
]

corelib_targets = []

# build T5
component = "t5"

t5 = SConscript(dirs = [os.path.join(os.getcwd(), "t5")], exports = full_import_list + " component ")

component = "core"

for d in corelibs:
  corelib_targets.append(SConscript(dirs = [d], exports = full_import_list + " component "))

graphiccommon = corelib_targets[0]
graphicalg    = corelib_targets[1]
configmanager = corelib_targets[2]

Depends(graphicalg, graphiccommon)
Depends(graphicalg, t5)
Depends(configmanager, t5)

turbosphere = SConscript(dirs = [os.path.join(os.getcwd(), "engine")], exports = full_import_list)

Depends(turbosphere, t5)
Depends(turbosphere, configmanager)
Depends(turbosphere, graphicalg)
Depends(turbosphere, graphiccommon)
