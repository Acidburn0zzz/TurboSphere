import os
import sys

defaultenv = Environment(ENV = os.environ)

defaultenv.SourceCode(".", None)

explicit_cc = ARGUMENTS.get('CC', '')
explicit_cxx = ARGUMENTS.get('CXX', '')

if explicit_cc:
  defaultenv.Replace(CC=explicit_cc)
if explicit_cxx:
  defaultenv.Replace(CXX=explicit_cxx)

jslib = "mozjs-40a1"

msvc_ccflags = "/O2 /EHsc /Zi /Fd${TARGET}.pdb /D__func__=__FUNCTION__ /DRELEASE_NO_PRINTING=1 "
msvc_cflags = ""
msvc_cxxflags = ""
msvc_linkflags = " /DEBUG "

gcc_ccflags = " -Wall -Werror -ffast-math -fno-rtti -fno-exceptions -march=native -Os -flto -Wcast-align -g "
gcc_cflags = " -std=c11 "
gcc_cxxflags = " -std=c++11 "
gcc_linkflags = " -lm "

if os.environ.get('TRAVIS') == 'true':
  gcc_cflags = " -std=c99 "
  gcc_cxxflags = " -std=gnu++0x "

clang_ccflags = gcc_ccflags + " -DHAS_UNISTD -DHAS_STAT_SYS -DUSE_STAT -Wno-return-type-c-linkage -Wno-mismatched-tags -Wno-invalid-offsetof"
clang_cflags = gcc_cflags
clang_cxxflags = gcc_cxxflags
clang_linkflags = " -lm -g "

#Checking platform.

def IsWindows():
  return sys.platform.startswith('win32') or sys.platform.startswith('win64')

def IsLinux():
  return sys.platform.startswith('linux')

def IsDarwin():
  return sys.platform == 'darwin'

def IsSolaris():
  return sys.platform.startswith('solaris') or sys.platform.startswith('sunos') or sys.platform.startswith('SunOS') or sys.platform.startswith('illumos')

def UsesGCC():
  return ((not IsWindows()) and (not IsDarwin())) or ('GCC' in ARGUMENTS)

compiler = ""

# Perform extra futzing for shared libraries on Unix platforms.

def LibraryInstall(name, library, env):
  LibraryFileName = os.path.join(os.getcwd(), library[0].name)
  if(IsDarwin()):
    print library[0].name
    return env.Command(os.path.join(name, library[0].name), LibraryFileName, "install_name_tool $SOURCE -id @executable_path/" + library[0].name + " && cp $SOURCE $TARGET ")
  else:
    return env.Install(name, library)

#Todo: Make these overrideable.
if IsWindows():
  compiler = 'msvc'
elif IsLinux():
  compiler = 'gcc'
elif IsDarwin():
  compiler = 'clang'
else:
  compiler = 'cc' # who knows.

if UsesGCC():
  compiler = 'gcc'

#Create a unified environment.
globalenv = defaultenv.Clone()

if os.name == 'posix':
  globalenv.Append(tools = ['default', 'textile'])

if compiler == 'msvc':
  globalenv.Replace(CCFLAGS = msvc_ccflags, CFLAGS = msvc_cflags, CXXFLAGS = msvc_cxxflags, LINKFLAGS = msvc_linkflags)
if compiler == 'gcc':
  globalenv.Replace(CCFLAGS = gcc_ccflags, CFLAGS = gcc_cflags, CXXFLAGS = gcc_cxxflags, LINKFLAGS = gcc_linkflags)
if compiler == 'clang':
  globalenv.Replace(CCFLAGS = clang_ccflags, CFLAGS = clang_cflags, CXXFLAGS = clang_cxxflags, LINKFLAGS = clang_linkflags)

def CheckForHeader(env, conf, header_name, macro_name):
  if conf.CheckCHeader(header_name) or conf.CheckCXXHeader(header_name):
    env.Append(CPPDEFINES = macro_name)
    return True
  return False

# Linux requires we check if the include files are in X/Open or SysV locations.
conf = Configure(globalenv.Clone())
if IsLinux() or (IsDarwin() and ( ARGUMENTS.get('use_unix_apis', 'n') == 'y' )):
  if CheckForHeader(globalenv, conf, 'tbb/concurrent_queue.h', "HAS_INTEL_TBB"):
    # Use Intel Thread Building Blocks whenever available.
    globalenv.Append(CPPDEFINES= "USE_INTEL_TBB")
  if not IsDarwin():
    CheckForHeader(globalenv, conf, 'time.h', "HAS_TIME")
  CheckForHeader(globalenv, conf, 'atomic', "HAS_ATOMIC")
  CheckForHeader(globalenv, conf, 'thread', "HAS_THREAD")
  CheckForHeader(globalenv, conf, 'mutex', "HAS_MUTEX")
  if CheckForHeader(globalenv, conf, 'stat.h', "HAS_UNISTD") or CheckForHeader(globalenv, conf, 'sys/stat.h', "HAS_UNISTD_SYS"):
    globalenv.Append(CPPDEFINES = "USE_UNISTD")
  if CheckForHeader(globalenv, conf, 'stat.h', "HAS_STAT") or CheckForHeader(globalenv, conf, 'sys/stat.h', "HAS_STAT_SYS"):
    globalenv.Append(CPPDEFINES = "USE_STAT")
  CheckForHeader(globalenv, conf, 'types', "HAS_TYPES")
  CheckForHeader(globalenv, conf, 'sys/types', "HAS_TYPES_SYS")

conf.Finish()

# OS X has singular define locations:
if IsDarwin():
    globalenv.Append(CPPDEFINES = ["HAS_UNISTD", "HAS_STAT_SYS", "USE_STAT"])
    globalenv.Append(CPPDEFINES = ["OS_X"]) # We could probably just trust __APPLE__ or __MACH__.
    #globalenv.Append(CPPPATH = ["/usr/local/include/"], LIBPATH = ["/usr/local/lib/"]) # Necessary on Yosemite, doesn't do any harm on Mavericks.

globalenv.Append(LIBPATH=[os.path.join(os.getcwd(), "src/thirdparty/lib")])

# Because Windows requires different functions entirely, it is implemented as a unique code path in
# the source code proper.

librarydir = os.path.join(os.getcwd(), "lib")
binarydir  = os.path.join(os.getcwd(), "bin")

localenv = globalenv.Clone()

# perform config.
libconfig = SConscript(dirs = ["lib"], exports = "globalenv localenv")

#because this was a config script, we copy back what it set.
globalenv = libconfig.Clone()
localenv  = libconfig.Clone()

# Begin the build proper.
build = SConscript(dirs = ["extra"], exports = "globalenv localenv IsLinux IsWindows IsDarwin IsSolaris UsesGCC LibraryInstall compiler librarydir binarydir jslib")
build = SConscript("src/Sconscript", exports = "globalenv localenv IsLinux IsWindows IsDarwin IsSolaris UsesGCC LibraryInstall compiler librarydir binarydir jslib")
instl = SConscript(dirs = ["bin"], exports = "globalenv localenv IsLinux IsWindows IsDarwin IsSolaris UsesGCC LibraryInstall compiler librarydir binarydir jslib")
